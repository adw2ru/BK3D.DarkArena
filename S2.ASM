	;
	; STARTUP INIT
HWINIT:	EMT 14
	MOV #INE,R1
	CLR R2
	EMT 20
	; GET BK TYPE&CORRECT MAIN LOOP
	CALL GETBKT
	;CLR BKTYPE		; DISABLE
	CALL EMAIN
	; SET STOP KEY VECTOR
	MOV #161716,@#2
	MOV #2,@#4
	; SET KEYBOARD VECTOR
	MOV #TN,@#274
	MOV #TI,@#60
	; SET TIMER VECTOR
	MOV #N100,@#100
	CLR @#102
	; SET IOT VECTOR
	MOV #N100,@#20		; USED IN BK0010 MODE
	CLR @#22
	; DETECT BAD PORT BITS
	MOV #1024.,R0
	CLR R1
1$:	BIS @#177714,R1
	SOB R0,1$
	MOV R1,JOYMSK
	RET
	;
	; EDIT CODE IN MAIN LOOP
EMAIN:	TST BKTYPE
	BNE AZZ
	; BK0010 WITHOUT AZBK
	MOV #1,(PC)+
TCC:	.WORD 4			; 6 FOR	AZ (TARGET 8-10FPS)
	RET
	; BK0010,BK0011,AZBK
AZZ:	MOV #240,ML2		; PUT NOP
	RET
	;
	; CHECK	BK HARDWARE
	; BKTYPE WILL BE:
	; BIT0=0 BK0010
	; BIT0=1 BK0011M
	; BIT1=1 AZBK FOUND
GETBKT:	CLR (PC)+
BKTYPE:	.WORD 0
	CMPB @#177717,#200	; BK0011
	BEQ 1$
	INC BKTYPE
1$:	MOV #2,R0		; AZBK
	MOV #AZTST,@#4
	BIS #4,@#177346		; TRY SET 48Hz
	NOP			; FOR REAL HW
	BIS R0,BKTYPE
	RET
AZTST:	CLR R0			; +2
	RTI
	;
	; KEYBOARD IRQ ROUTINES
TN:	MOV R0,-(SP)
	MOVB @#177662,R0
	BNE 2$
	TST BKTYPE
	BEQ 2$
	INC TCC			; MACHINE SPEED	SETUP
	CMP TCC,#6
	BLOS 2$
	MOV #3,TCC		; 3,4,5,6
2$:	CMPB R0,#100
	BLO 1$
	BICB #40,R0
1$:	ADD #200,R0
	MOVB R0,KEYCOD
	MOV (SP)+,R0
	RTI
TI:	MOV R0,-(SP)
	MOVB @#177662,R0
	CMPB R0,#100
	BLO 1$
	BICB #40,R0
1$:	MOVB R0,KEYCOD
	MOV (SP)+,R0
	RTI
	;
	; SOUND	FUNCTION CALL
SNDF:	MTPS #340
	CALL @SNDPR
	MTPS #0
	RET
	;
	; PLAY SND EFFECT
	; R2=POINTER TO	EFFECT
PLEF:	MOV (R2)+,(PC)+
TEMPO:	.WORD 0
	MOV (R2)+,(PC)+
TONE:	.WORD 0
	MOV #SNDR,(PC)+
SNDPR:	.WORD SNDN
	MOV #32,@#177712
	RET
	;
TFIX:	MOV #SNDR,SNDPR		; TURBO6,7 BUG WORKAROUND
	RET
	;
SNDR:	TST @#177710
	BPL 1$
	DEC TEMPO
	BEQ NOSND
	MOV @#177710,@#177706
	ADD TONE,@#177706
	MOV #32,@#177712
	MOV @#102064,@#177716
	MOV #SNDS,SNDPR
1$:	RET
	;
SNDS:	TST @#177710
	BPL 1$
	DEC TEMPO
	BEQ NOSND
	MOV @#177710,@#177706
	ADD TONE,@#177706
	MOV #32,@#177712
	MOV @#102076,@#177716
	BR TFIX
	;MOV #SNDR,SNDPR
1$:	RET
	;
NOSND:	MOV #20,@#177712
	CLR @#177706
	MOV #SNDN,SNDPR
SNDN:	RET
	;
	; PROCESS SOUND	EFFECTS	BITMAP
PRCSEF:	MTPS #340
	MOV (PC)+,R5
SNDBM:	.WORD 0
	BIT #32.,R5
	BEQ 1$
	; SND.EF.1
	MOV #EFF1,R2
	CALL PLEF
	BR 6$
1$:	BIT #16.,R5
	BEQ 2$
	; SND.EF.2
	MOV #EFF2,R2
	CALL PLEF
	BR 6$
2$:	BIT #8.,R5
	BEQ 3$
	; SND.EF.3
	MOV #EFF3,R2
	CALL PLEF
	BR 6$
3$:	BIT #4.,R5
	BEQ 4$
	; SND.EF.4
	MOV #EFF4,R2
	CALL PLEF
	BR 6$
4$:	BIT #2.,R5
	BEQ 5$
	; SND.EF.5
	MOV #EFF5,R2
	CALL PLEF
	BR 6$
5$:	BIT #1.,R5
	BEQ 6$
	; SND.EF.6
	MOV #EFF6,R2
	CALL PLEF
6$:	MTPS #0
	RET
	;
	; START	TIMER
STRTIM:	MOV TCC,TC
	CLRB TKCNTR
	CLRB PRCNTR
	TST BKTYPE		; BK0010? YES->NO TIMER
	BEQ 1$
	BIT #2,BKTYPE		; AZBK?	NO->BK0011 TIMER
	BEQ 2$
	BIS #10,@#177346
	BR 1$
2$:	BIC #40000,@#177662	; BK0011
1$:	RET
	;
	; STOP TIMER
STPTIM:	TST BKTYPE		; BK0010? YES->NO TIMER
	BEQ 1$
	BIT #2,BKTYPE		; AZBK?	NO->BK0011 TIMER
	BEQ 2$
	BIC #10,@#177346	; AZBK
	BR 1$
2$:	BIS #40000,@#177662	; BK0011
1$:	RET
	;
	; TIMER	THREAD
N100:	DEC (PC)+
TC:	.WORD 0
	BEQ 1$
	RTI
1$:	MOV TCC,TC
	BIT #40000,BITFLD
	BEQ 3$
	RTI
3$:	BIS #40000,BITFLD
	MOV R0,-(SP)
	MOV R1,-(SP)
	MOV R2,-(SP)
	MOV R3,-(SP)
	MOV R4,-(SP)
	MOV R5,-(SP)
	INCB TKCNTR
	CMP #GSPAUS,GSTATE
	BEQ 4$
	CALL MNMOV	; MONSTERS MOVE
4$:	CALL JOY	; INPUT&PLAYER MOVE
	CMP #GSHTWN,GSTATE
	BNE 2$
	CALL SETTA
	MOV MNBF+6.,R1
	MOV MNBF+4.,R0
	CALL SETPL
2$:	MOV (SP)+,R5
	MOV (SP)+,R4
	MOV (SP)+,R3
	MOV (SP)+,R2
	MOV (SP)+,R1
	MOV (SP)+,R0
	BIC #40000,BITFLD
	RTI
	;
SYNCP:	MTPS #340
	; SYNC PLAYER
	MOV PLX,SPLX
	MOV PLY,SPLY
	MOV PLA,SPLA
	MTPS #0
	RET
	;
	.END
